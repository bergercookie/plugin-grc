# These methods override the default calls to append `grc exec`.
# Please send a message if you find another command that can be
# used with `grc`.
# vim: ft=fish
set -l execs cat           \
             cvs           \
             diff          \
             dig           \
             gcc           \
             g++           \
             ls            \
             make          \
             mount         \
             mtr           \
             netstat       \
             ping          \
             ps            \
             tail          \
             traceroute    \
             wdiff

if set -q grc_plugin_execs
  set execs $execs $grc_plugin_execs
end

for executable in $execs
  set optionsvariable "grcplugin_"$executable
  set options $$optionsvariable
  if type -q $executable
    function $executable --inherit-variable executable --inherit-variable options
      __execute_as_grc $executable $argv $options
    end
  end
end

function __execute_as_grc
  command grc -es --colour=auto $argv
end
